#!/usr/bin/env perl
use strict;
package App::perlbrew;

local $\ = "\n";

sub run_command {
    my (undef, $x, @args) = @_;
    my $self = bless {}, __PACKAGE__;

    $x ||= "help";
    my $s = $self->can("run_command_$x") or die "Unknow command: `$x`. Typo?\n";
    $self->$s(@args);
}

sub run_command_help {
    print <<HELP;

Usage:

    pb init
    pb install perl-5.11.1
    pb switch perl-5.11.1

HELP
}

sub run_command_init {
    require File::Path;
    File::Path::make_path(
        "$ENV{HOME}/.pb/perls",
        "$ENV{HOME}/.pb/dists",
        "$ENV{HOME}/.pb/build",
        "$ENV{HOME}/.pb/etc",
        "$ENV{HOME}/.pb/bin"
    );

    system <<RC;
echo 'export PATH=$ENV{HOME}/.pb/perls/bin:$ENV{HOME}/.pb/perls/current/bin:\${PATH}' > $ENV{HOME}/.pb/etc/bashrc
RC

    print "Perl Environmet Initiated";
}

sub run_command_install {
    my ($self, $dist) = @_;

    my ($dist_name, $dist_version) = $dist =~ m/^(.*)-([\d.]+)$/;
    if ($dist_name eq 'perl') {
        require LWP::UserAgent;
        my $ua = LWP::UserAgent->new;

        print "Fetching $dist...";
        my $r = $ua->get("http://search.cpan.org/dist/$dist");
        die "Fail to fetch the dist of $dist." unless $r->is_success;

        my $html = $r->content;
        my ($dist_path, $dist_tarball) = $html =~ m[<a href="(/CPAN/authors/id/.+/(${dist}.tar.(gz|bz2)))">Download</a>];

        $r = $ua->get("http://search.cpan.org/${dist_path}",
                      ":content_file" => "$ENV{HOME}/.pb/dists/${dist_tarball}");
        die "Fail to fetch the dist of $dist." unless $r->is_success;

        my $usedevel = $dist_version =~ /5\.11/ ? "-Dusedevel" : "";
        print "Installing $dist...";

        my $tarx = "tar " . ($dist_tarball =~ /bz2/ ? "xjf" : "xzf");
        system(join ";",
            "cd $ENV{HOME}/.pb/build",
            "$tarx $ENV{HOME}/.pb/dists/${dist_tarball}",
            "cd $dist",
            "rm -f config.sh Policy.sh",
            "sh Configure -de -Dprefix=$ENV{HOME}/.pb/perls/$dist ${usedevel}",
            "make",
            "make test && make install"
        );
    }
}

sub run_command_switch {
    my ($self, $dist) = @_;
    die "${dist} is not installed\n" unless -d "$ENV{HOME}/.pb/perls/${dist}";
    my ($dist_name, $dist_version) = $dist =~ m/^(.*)-([\d.]+)$/;
    unlink "$ENV{HOME}/.pb/perls/current";
    system "cd $ENV{HOME}/.pb/perls; ln -s $dist current";
    for my $executable (<$ENV{HOME}/.pb/perls/current/bin/*${dist_version}>) {
        my ($name) = $executable =~ m/bin\/(.+)${dist_version}/;
        system("ln -fs $executable $ENV{HOME}/.pb/bin/${name}");
    }
}

package main;
App::perlbrew->run_command(@ARGV);

__END__

=head1 NAME

pb - Perl Environment manager.

=head1 SYNOPSIS

    pb init

    pb install perl-5.8.1
    pb install perl-5.11.5

    pb switch perl-5.11.5
    perl -v

    pb switch perl-5.8.1
    perl -v

=head1 AUTHOR

Kang-min Liu  C<< <gugod@gugod.org> >>

=head1 LICENCE AND COPYRIGHT

Copyright (c) 2010, Kang-min Liu C<< <gugod@gugod.org> >>.

This is free software, licensed under:

    The MIT (X11) License

=head1 DISCLAIMER OF WARRANTY

BECAUSE THIS SOFTWARE IS LICENSED FREE OF CHARGE, THERE IS NO WARRANTY
FOR THE SOFTWARE, TO THE EXTENT PERMITTED BY APPLICABLE LAW. EXCEPT WHEN
OTHERWISE STATED IN WRITING THE COPYRIGHT HOLDERS AND/OR OTHER PARTIES
PROVIDE THE SOFTWARE "AS IS" WITHOUT WARRANTY OF ANY KIND, EITHER
EXPRESSED OR IMPLIED, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED
WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE. THE
ENTIRE RISK AS TO THE QUALITY AND PERFORMANCE OF THE SOFTWARE IS WITH
YOU. SHOULD THE SOFTWARE PROVE DEFECTIVE, YOU ASSUME THE COST OF ALL
NECESSARY SERVICING, REPAIR, OR CORRECTION.

IN NO EVENT UNLESS REQUIRED BY APPLICABLE LAW OR AGREED TO IN WRITING
WILL ANY COPYRIGHT HOLDER, OR ANY OTHER PARTY WHO MAY MODIFY AND/OR
REDISTRIBUTE THE SOFTWARE AS PERMITTED BY THE ABOVE LICENCE, BE
LIABLE TO YOU FOR DAMAGES, INCLUDING ANY GENERAL, SPECIAL, INCIDENTAL,
OR CONSEQUENTIAL DAMAGES ARISING OUT OF THE USE OR INABILITY TO USE
THE SOFTWARE (INCLUDING BUT NOT LIMITED TO LOSS OF DATA OR DATA BEING
RENDERED INACCURATE OR LOSSES SUSTAINED BY YOU OR THIRD PARTIES OR A
FAILURE OF THE SOFTWARE TO OPERATE WITH ANY OTHER SOFTWARE), EVEN IF
SUCH HOLDER OR OTHER PARTY HAS BEEN ADVISED OF THE POSSIBILITY OF
SUCH DAMAGES.



